buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.8'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:recyclerview-v7:21.0.+'
    compile 'com.android.volley:volley:1.0.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

android {
    compileSdkVersion 'android-24'
    buildToolsVersion '24.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode "git rev-list --count HEAD".execute().text as int
        versionName "git describe --tags".execute().text[1..-2] // strip v and newline
    }

    buildTypes
    {
        debug
        {
            applicationIdSuffix ".debug"
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "version", "v" + variant.versionName + " (" + variant.versionCode + ")"
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    // processConfigurations = ['compile', 'extra']
    // includes = ['packages.md', 'extra.md']
    // samples = ['samples/basic.kt', 'samples/advanced.kt']
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/darkwater/hanze-schedule/blob/master/src/main/kotlin"
        suffix = "#L"
    }
    sourceDirs = files('src/main/kotlin')
}

task generateIcons {
    doLast {
        def dpiFactors = [
            mdpi:   1.0,
            hdpi:   1.5,
            xhdpi:  2.0,
            xxhdpi: 3.0
        ]

        [ "main", "debug" ].each { buildType ->
            dpiFactors.each { spec, factor ->
                def output = "src/$buildType/res/drawable-$spec/ic_launcher.png"
                def dpi = 22.50 * factor
                def sed = buildType == "main" ? 'n' : 's/#ee6600/#607d8b/'

                [ "sh", "-c",
                    "sed '$sed' assets/icon.svg | inkscape --export-png='$output' /dev/stdin --export-dpi=$dpi"
                ].execute()
            }
        }
    }
}
