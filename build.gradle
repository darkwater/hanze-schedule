buildscript {
    ext.kotlin_version = '1.0.4'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.android.support:design:24.2.1"
    compile "com.android.volley:volley:1.0.0"
}

android {
    compileSdkVersion 'android-24'
    buildToolsVersion '24.0.3'

    defaultConfig {
        versionCode "git rev-list --count HEAD".execute().text as int
        versionName "git describe --tags".execute().text[1..-2] // strip v and newline
    }

    buildTypes
    {
        debug
        {
            applicationIdSuffix ".debug"
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "version", "v" + variant.versionName + " (" + variant.versionCode + ")"
    }
}

task generateIcons {
    doLast {
        def dpiFactors = [
            mdpi:   1.0,
            hdpi:   1.5,
            xhdpi:  2.0,
            xxhdpi: 3.0
        ]

        [ "main", "debug" ].each { buildType ->
            dpiFactors.each { spec, factor ->
                def output = "src/$buildType/res/drawable-$spec/ic_launcher.png"
                def dpi = 22.50 * factor
                def sed = buildType == "main" ? 'n' : 's/#ee6600/#607d8b/'

                [ "sh", "-c",
                    "sed '$sed' assets/icon.svg | inkscape --export-png='$output' /dev/stdin --export-dpi=$dpi"
                ].execute()
            }
        }
    }
}
